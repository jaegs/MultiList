namespace Jaegs
	use System.Collections
	class CountedSet<of T> #implements ICollection<of T>, IEnumerable<of T>
		"""
		This class is a CountedSet (AKA multiset) container. Unlike a set, a multiset allows the same
		item to be inserted multiple times.
		For a computer science background, see
		http://en.wikipedia.org/wiki/Set_(computer_science)#Multiset.
		For a mathematical background, see
		http://en.wikipedia.org/wiki/Multiset

		To get the number of times that a particular
		item is in the CountedSet, use count = CountedSet[item].
		Adding an item to the CountedSet will increase it's count by one
		As an invariant the count of an item must be >0 for all items
		Removing an item decreases the count by one
		Use removeAll to delete all occurances of a particular item.
		"""
	
		var 	_data as IDictionary<of T, int>

		cue init
			base.init
			_data = Dictionary<of T, int>()
		
		cue init(items as T*)
			.init
			.addRange(items)

		cue init(other as CountedSet<of T>)
			.init
			.addRange(other)

		cue init(other as IDictionary<of T, int>)
			.init
			.addRange(other)

		pro [item as T] as int
			"""
			If the item is not in the CountedSet, .get returns 0
			"""
			get
				return _data.get(item, 0)
			set
				require value > 0
				_data[item] = value

		get count as int
			"""
			Total number of items in the CountedSet
			Equivalent to the sum of counts
			"""
			sum = 0
			for kvp in .counts, sum += kvp.value
			return sum

		get uniqueCount
			"""
			Returns the total number of unique items
			in the CountedSet.
			"""
			return _data.count
			
		get isReadOnly as bool
			return false

		get counts as KeyValuePair<of T, int>*
			"""
			In the returned Stream of KeyValuePairs,
			the key is the item
			and the value is the item's count
			"""
			for item, count in _data
				yield KeyValuePair<of T, int>(item, count)
			
		def contains(item as T) as bool
			return _data.keys.contains(item)

		def items as IEnumerator<of T>
			for item, count in _data, for c in count, yield item

		def getEnumerator as System.Collections.IEnumerator
			implements System.Collections.IEnumerable
			return .getEnumerator

		def getEnumerator as IEnumerator<of T>
			return _data.keys.getEnumerator

		cue enumerate as T*
			for item, count in _data, for c in count, yield item
	
		def toList as IList<of T>
			return List<of T>(.enumerate)

		def uniqueSet as Cobra.Core.ISet<of T>
			return Set<of T>(_data.keys)

		def add(item as T)
			"""
			Adding an instance of an item is 
			equivalent to increasing the item's count by one 
			"""
			this[item] += 1

		def add(item as T, num as int)
			"""
			Adds num instances of item to the CountedSet
			This is equivalent to increasing the item's count by num.
			Num must be >0.
			"""
			require num > 0
			this[item] += num
			
		def addRange(other as CountedSet<of T>)
			for item, count in other.counts, .add(item, count)

		def addRange(other as IDictionary<of T, int>)
			for item, count in other, .add(item, count)
	
		def addRange(items as T*)
			for item in items, .add(item)

		def remove(item as T) as bool
			"""
			Removes one instance of item from the CountedSet
			This is equivalent to subtracting the item's count by one
			"""
			return .remove(item, 1)

		def remove(item as T, num as int) as int
			"""
			Removes up to num instances of item from the CountedSet
			This is equivalent to subtracting the item's count by num
			Returns the actual number of items removed
			"""
			if .contains(item)
				maxNumRemoved = this[item]
				this[item] -= num
				if this[item] <= 0
					_data.remove(item)
					return maxNumRemoved
				return num
			return 0

		def removeAll(item as T) as bool
			"""Removes all occurances of item from the CountedSet """
			return _data.remove(item)


		def clear
			"""Removes all items from the CountedSet """
			_data.clear

		def copyTo(array as T[], arrayIndex as int)
			_data.keys.copyTo(array, arrayIndex)

		def equals(other as Object?) as bool is override
			if not other inherits CountedSet<of T>, return false
			return _data == (other to CountedSet<of T>)._data

		def getHashCode as int is override
			"""
			As a completely mutable object, CountedSet does not
			support getHashCode
			"""
			throw NotSupportedException()

		def toString as String is override
			sb = StringBuilder().append(c'{')
			pos = 0
			count = .count
			for item in .enumerate
				sb.append(item)
				if pos < count - 1, sb.append(", ")
				pos += 1
			sb.append(c'}')
			return sb.toString

		#Multiset math operations

		def sum(other as CountedSet<of T>) as CountedSet<of T>
			"""
			Adds the counts of each CountedSet
			"""
			answer = CountedSet<of T>(this)
			answer.addRange(other)
			return answer
			
		def difference(other as CountedSet<of T>) as CountedSet<of T>
			"""
			Subtracts the counts of other from this CountedSet.
			Only keeps items with count > 0
			"""
			answer = CountedSet<of T>(this)
			for item, count in .counts
				count = count #suppress warning
				if other.contains(item)
					newCount = answer[item] - other[item]
					if newCount > 0, answer[item] = newCount
					else, answer.removeAll(item)
			return answer
					
		def union(other as CountedSet<of T>) as CountedSet<of T>
			"""
			Take the max count of each item from each CountedSet
			"""
			answer = CountedSet<of T>(this)
			for item, count in other.counts
				if answer[item] < count, answer[item] = count
			return answer

		def intersection(other as CountedSet<of T>) as CountedSet<of T>
			"""
			Takes the min count of each item from each CountedSet
			"""
			answer = CountedSet<of T>()
			for item, count in .counts
				if other.contains(item)
					answer[item] = Math.min(count, other[item])
			return answer

		def mostCommon(num as int) as ICollection<of T>
			throw NotImplementedException()

		/#
		These probably don't have much practical use but could be implemented
		def isSubsetOf(s as CountedSet<of T>) as bool
		def isSupersetOf(s as CountedSet<of T>) as bool
		def product(other as CountedSet<of T>) as bool
		def multiply(n as int)
		def symmetricDifference(s as CountedSet<of T>) as CountedSet<of T>
		#/

	class TestCountedSet
		test
			CountedSet = CountedSet<of char>('mississippi')
			assert CountedSet.toList.sorted == [c'i', c'i', c'i', c'i', c'm', c'p', c'p', c's', c's', c's', c's'] 
			assert CountedSet[c'm'] == 1
			assert CountedSet[c'i'] == 4
			assert CountedSet[c'p'] == 2
			assert CountedSet[c's'] == 4

		test sum
			CountedSet1 = CountedSet<of int>([1, 2, 2])
			CountedSet2 = CountedSet<of int>([0, 2, 2])
			sumCountedSet = CountedSet1.sum(CountedSet2)
			assert sumCountedSet[1] == 1 and sumCountedSet[2] == 4 and sumCountedSet[0] == 1

		test union
			CountedSet1 = CountedSet<of int>([1, 2, 2])
			CountedSet2 = CountedSet<of int>([1, 1, 2])
			unionCountedSet = CountedSet1.union(CountedSet2)
			assert unionCountedSet[1] == 2 and unionCountedSet[2] == 2

		test intersection
			CountedSet1 = CountedSet<of int>([1, 2, 2, 3])
			CountedSet2 = CountedSet<of int>([1, 1, 2])
			interB = CountedSet1.intersection(CountedSet2)
			assert interB[1] == 1 and interB[2] == 1 and interB[3] == 0

		test difference
			CountedSet1 = CountedSet<of int>([1, 2, 2, 3, 3, 3])
			CountedSet2 = CountedSet<of int>([1, 1, 2, 3])
			diffCountedSet = CountedSet1.difference(CountedSet2)
			assert diffCountedSet[1] == 0 and diffCountedSet[2] == 1 and diffCountedSet[3] == 2
			assert not diffCountedSet.contains(1)

		test toString
			CountedSet = CountedSet<of char>('mississippi')
			assert CountedSet.toString == '{m, i, i, i, i, s, s, s, s, p, p}'

		test removeAll
			CountedSet = CountedSet<of char>('mississippi')
			CountedSet.removeAll(c'i')
			assert not CountedSet.contains(c'i')

		/#test set
			CountedSet= CountedSet<of int>()
			expect Cobra.Core.RequireException, CountedSet[3] = -2 #/

		test uniqueSet
			CountedSet = CountedSet<of int>([1, 1, 1, 1, 2, 2, 2, 3])
			assert CountedSet.uniqueSet == Set<of int>([1, 2, 3])
				
		def main
			pass
	
		
			
